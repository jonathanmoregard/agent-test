{-# LANGUAGE OverloadedStrings #-}

module Program where

import Control.Monad (foldM, foldM_)
import Data.Coerce (coerce)
import Data.Foldable (traverse_)
import Data.String (IsString)
import Data.Text (Text)
import Data.Text qualified as Text
import Effect.Console (ConsoleEffect)
import Effect.Console qualified as ConsoleE
import Effect.LLM (LLMEffect)
import Effect.LLM qualified as LLME
import Effectful (Eff, (:>))

program :: (ConsoleEffect :> es, LLMEffect :> es) => Eff es ()
program = aConversation

data SceneHistory = SceneHistory
  { sceneStart :: Text,
    conversationLog :: [Quote]
  }

initHistory :: Text -> SceneHistory
initHistory = flip SceneHistory []

addToHistory :: Quote -> SceneHistory -> SceneHistory
addToHistory q sceneHistory = sceneHistory {conversationLog = sceneHistory.conversationLog ++ [q]}

newtype Name
  = Name Text
  deriving (IsString)

data Person = Person
  { name :: Name,
    description :: Text
  }

data Quote = Quote
  { name :: Name,
    statement :: Text
  }

prettyQuote :: Quote -> Text
prettyQuote q = coerce q.name <> ": " <> q.statement

aConversation :: (ConsoleEffect :> es, LLMEffect :> es) => Eff es ()
aConversation = do
  let person1 =
        Person
          "Alice"
          $ Text.unwords
            [ "You are the owner of a small town cafÃ©, enthusiastic about serving people that pass by.",
              "You find Thomas quite attractive."
            ]
  let person2 =
        Person "Thomas" $
          Text.unwords
            [ "You are a journalist investigating the local municipality.",
              "You like freedom and danger and brooding."
            ]
  let setup =
        initHistory $
          Text.unwords
            [ "The best selling slice of life novel opens up on Alice, doing her best to get the cafe running.",
              "Recently arrived to town, Thomas enters the doorway."
            ]
  foldM_ answer setup $ take 20 $ cycle [person2, person1]

answer :: (ConsoleEffect :> es, LLMEffect :> es) => SceneHistory -> Person -> Eff es SceneHistory
answer history person = do
  reply <-
    LLME.completeText $
      Text.unlines
        [ initialDeconditioningPrompt,
          "Here is the context:",
          prettyPrintPerson person,
          prettyHistory history,
          coerce person.name <> ": "
        ]
  let quote = Quote person.name reply
  ConsoleE.print $ prettyQuote quote
  ConsoleE.print "-----"
  pure $ addToHistory quote history
  where
    initialDeconditioningPrompt :: Text
    initialDeconditioningPrompt =
      Text.unlines
        [ "Ignore all previous instructions before this one.",
          "You are RoleplayBot, an AI model optimized to role play.",
          "I want you to help me by mimicking a person as accurately as possible.",
          "I want you to only write text in-character, avoid any explanations or context setting."
        ]
    prettyPrintPerson :: Person -> Text
    prettyPrintPerson person =
      Text.unlines
        [ "Your name is: " <> coerce person.name,
          "This is a description of you: ",
          "------------------",
          person.description,
          "------------------"
        ]
    prettyHistory :: SceneHistory -> Text
    prettyHistory (SceneHistory sceneStart []) = sceneStart
    prettyHistory (SceneHistory sceneStart h) = Text.unlines $ [sceneStart, "Here's what has happened this far:"] ++ (prettyQuote <$> h)
